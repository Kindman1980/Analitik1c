ВЫБРАТЬ
	ПрофилиГруппДоступаРоли.Ссылка КАК Профиль,
	ГруппыДоступа.Ссылка КАК ГруппаДоступа
ПОМЕСТИТЬ ВТ_ПрофилиИГД
ИЗ
	Справочник.ПрофилиГруппДоступа.Роли КАК ПрофилиГруппДоступаРоли
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа КАК ГруппыДоступа
		ПО (ПрофилиГруппДоступаРоли.Ссылка = ГруппыДоступа.Профиль)
ГДЕ
	ПрофилиГруппДоступаРоли.Роль = &Роль
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ПрофилиИГД.Профиль КАК Профиль,
	ВТ_ПрофилиИГД.ГруппаДоступа КАК ГруппаДоступа,
	ВЫБОР
		КОГДА ПрофилиГруппДоступаВидыДоступа.Ссылка ССЫЛКА Справочник.ПрофилиГруппДоступа
			ТОГДА "Да"
		ИНАЧЕ "нет"
	КОНЕЦ КАК ОграничениеОрганизация,
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ГруппыДоступаПользователи.Пользователь) КАК ЧислоПользователей
ПОМЕСТИТЬ ВТ_ГруппыДоступаИОграничения
ИЗ
	ВТ_ПрофилиИГД КАК ВТ_ПрофилиИГД
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.ВидыДоступа КАК ПрофилиГруппДоступаВидыДоступа
		ПО ВТ_ПрофилиИГД.Профиль = ПрофилиГруппДоступаВидыДоступа.Ссылка
			И (ПрофилиГруппДоступаВидыДоступа.ВидДоступа ССЫЛКА Справочник.Организации)
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		ПО ВТ_ПрофилиИГД.ГруппаДоступа = ГруппыДоступаПользователи.Ссылка

СГРУППИРОВАТЬ ПО
	ВТ_ПрофилиИГД.Профиль,
	ВТ_ПрофилиИГД.ГруппаДоступа,
	ВЫБОР
		КОГДА ПрофилиГруппДоступаВидыДоступа.Ссылка ССЫЛКА Справочник.ПрофилиГруппДоступа
			ТОГДА "Да"
		ИНАЧЕ "нет"
	КОНЕЦ

ИМЕЮЩИЕ
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ГруппыДоступаПользователи.Пользователь) <> 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ГруппыДоступаИОграничения.ГруппаДоступа КАК ГруппаДоступа,
	ВТ_ГруппыДоступаИОграничения.ОграничениеОрганизация КАК ОграничениеОрганизация,
	ГруппыДоступаПользователи.Пользователь КАК Пользователь
ПОМЕСТИТЬ ВТ_ГруппыДоступаГруппыПользователей
ИЗ
	ВТ_ГруппыДоступаИОграничения КАК ВТ_ГруппыДоступаИОграничения
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		ПО ВТ_ГруппыДоступаИОграничения.ГруппаДоступа = ГруппыДоступаПользователи.Ссылка
ГДЕ
	ГруппыДоступаПользователи.Пользователь ССЫЛКА Справочник.ГруппыПользователей
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ГруппыДоступаГруппыПользователей.ГруппаДоступа КАК ГруппаДоступа,
	ВТ_ГруппыДоступаГруппыПользователей.ОграничениеОрганизация КАК ОграничениеОрганизация,
	ГруппыПользователейСостав.Пользователь КАК Пользователь
ПОМЕСТИТЬ ВТ_ГруппыИПользователи
ИЗ
	ВТ_ГруппыДоступаГруппыПользователей КАК ВТ_ГруппыДоступаГруппыПользователей
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
		ПО (ВТ_ГруппыДоступаГруппыПользователей.Пользователь = ГруппыПользователейСостав.Ссылка)

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВТ_ГруппыДоступаИОграничения.ГруппаДоступа,
	ВТ_ГруппыДоступаИОграничения.ОграничениеОрганизация,
	ГруппыДоступаПользователи.Пользователь
ИЗ
	ВТ_ГруппыДоступаИОграничения КАК ВТ_ГруппыДоступаИОграничения
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
		ПО ВТ_ГруппыДоступаИОграничения.ГруппаДоступа = ГруппыДоступаПользователи.Ссылка
ГДЕ
	ГруппыДоступаПользователи.Пользователь ССЫЛКА Справочник.Пользователи
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ГруппыИПользователи.ГруппаДоступа КАК ГруппаДоступа,
	ВТ_ГруппыИПользователи.ОграничениеОрганизация КАК ОграничениеОрганизация,
	ВТ_ГруппыИПользователи.Пользователь КАК Пользователь
ИЗ
	ВТ_ГруппыИПользователи КАК ВТ_ГруппыИПользователи

СГРУППИРОВАТЬ ПО
	ВТ_ГруппыИПользователи.ГруппаДоступа,
	ВТ_ГруппыИПользователи.ОграничениеОрганизация,
	ВТ_ГруппыИПользователи.Пользователь
